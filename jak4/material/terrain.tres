[gd_resource type="Shader" format=2]

[resource]
code = "shader_type spatial;
render_mode cull_back, depth_draw_opaque, world_vertex_coords, async_hidden;

uniform sampler2D wall: hint_albedo;
uniform sampler2D ground_green: hint_albedo;
uniform sampler2D ground_black: hint_albedo;
uniform sampler2D ceiling: hint_albedo;
uniform float uv_scale = 0.125;
uniform float uv_scale_black = 0.17;
uniform float power = 5.0;
uniform float softness: hint_range(0.0, 1.0, 0.1) = 0.5;
uniform float specularity_ground: hint_range(1, 32) = 1.0;
uniform float specularity_wall: hint_range(1, 32) = 1.0;
uniform float specularity_ceiling: hint_range(1, 32) = 1.0;
uniform float light_bias : hint_range(-1, 1) = 0.0;
uniform float normal_offset : hint_range(-15.0, 15.0, 0.1) = 0.0;
uniform float shadow_normal_parallel : hint_range(1, 100, 0.5) = 10.0;

varying vec3 position;
varying vec3 normal;
varying float specularity;

void vertex() {
	position = VERTEX.xyz;
	normal = NORMAL;
}

void fragment() {
	SHADOW_NORMAL_OFFSET = normal_offset;
	SHADOW_NORMAL_PARALLEL = shadow_normal_parallel;
	vec3 n = normalize(normal);
	vec4 color_x = texture(wall, position.zy*uv_scale);
	vec4 color_z = texture(wall, position.xy*uv_scale);
	vec4 color_y_down = texture(ceiling, position.xz*uv_scale);
	
	float y_pow = sign(n.y)*pow(abs(n.y), power);
	
	vec4 color_green = texture(ground_green, position.xz*uv_scale);
	vec4 color_black = texture(ground_black, position.xz*uv_scale_black);
	float ground_mix = COLOR.g;
	vec4 color_y_up = mix( color_black, color_green,
		ground_mix);
		
	vec4 color =
		color_x*pow(n.x, 2) 
		+ color_z*pow(n.z, 2)
		+ color_y_up*max(y_pow, 0.0)
		+ color_y_down*max(-y_pow, 0.0);
	specularity = 
		specularity_wall*(pow(n.z, 2) + pow(n.x, 2))
		+ specularity_ground*max(y_pow, 0.0)
		+ specularity_ceiling*max(-y_pow, 0.0);
	ALBEDO = clamp(color.rgb, vec3(0.0), vec3(1.0));
	ROUGHNESS = clamp(2.0/(specularity + 0.01), 0, 1);
}

void light()
{
	// negative. Use as ambient shadow
	if(LIGHT_COLOR.r < 0.0 || LIGHT_COLOR.g < 0.0 || LIGHT_COLOR.b < 0.0) {
		DIFFUSE_LIGHT += (DIFFUSE_LIGHT + AMBIENT_LIGHT*ALBEDO)*LIGHT_COLOR*ATTENUATION;
	}
	else {
		float spec = specularity/32.0;
		float light = smoothstep(0, softness, dot(NORMAL, LIGHT) + light_bias);
		DIFFUSE_LIGHT += 0.4*(1.0-spec) * light * LIGHT_COLOR * ATTENUATION * ALBEDO;
		
		// Specular
		vec3 h = normalize(VIEW + LIGHT);
		float cNdotH = max(0.0, dot(NORMAL, h));
		float blinn = pow(cNdotH, specularity);
		blinn *= spec;
		float intensity = blinn; 
		DIFFUSE_LIGHT += LIGHT_COLOR * intensity * ATTENUATION * ALBEDO;
	}
}"
