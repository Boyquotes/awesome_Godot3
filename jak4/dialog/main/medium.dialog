:_start
* ?{mum/end} The Medium is eerily silent.
	> Hello?
		* There is no response.
	> {skip()} (Leave)
	{exit()}
{otherwise}!{medium/activated}
	* You've read about this.
	* To activate the medium, you just sit in the chair.
	* Do you want to activate the Medium?
	> {skip()} (Yes)
		{main_speaker.sit()}
			* ...
			* Nothing seems to happen.
			[You] Drat! Maybe I need to talk with Armstrong again. Perhaps we-
			{main_speaker.activate()}
			{mention("activated")}
			[You] ...
				My God...
	> {skip()} (No)
		{exit()}
{otherwise}
	{main_speaker.sit()}
	{mention("activated")}
	{main_speaker.activate()}

:mum
{mention("main_area")}

// Let's enumerate everything the player might know so far:
	// introvert - very reserved, few friends
	// intensity - austere, hardened by the war
	// abandon - tends to leave without saying goodbye
	// soldier - fought in the Vaeler war to defend Crab Hill
	// killer - not just a soldier, she killed many Vaelers
	// plateau - she lived on the Sylda Plateau before the war
	// crab hill - she lived on Crab Hill during the war
	// huh - she said "huh" and other nonverbal utterances frequently
	// lowlands - she moved to the lowlands after the war
	// caves - she was fond of caves and frequently explored them
// Should I enumerate how Jackie's state of mind changes? How does that affect the AI? The Medium canonically accounts for it, but does it do so perfectly?

!{mum/introvert}!{mum/intensity}
	// Special case when she has no personality
	{mention("head_empty")}

{main_speaker.show_mum()}
{mention("mum")}
* You hear something behind you.
* ...

!{mum/info}
	!{talked/mum}
		{remember("Mum. It's really her. At least, what I can remember. But it's barely anything. I have to find out where she lived, or who knew her around here. I think that will help...")}
		{Global.note_task("mum", "The Medium really works. But I don't know enough about her to have a conversation.")}
		{Global.add_stat("mum/info_needed")}
		!{mum/appearance}
			* The thing sitting next to you is barely human.
			* The outline of a dress. Her arched nose. The faint hum of her voice, whispering something less than words.
			* That's all you can make out. Somehow this is more than you could recall.
			* It's unmistakable.
		{otherwise}
			* Combined with the photograph you found, the face is unmistakable.
			* She stares blankly ahead.
		[You] Hi Mum.
			I....
		{player.anim_play("SitSad", "SittingSad-loop")} 
		[You] ...
			It works. It actually works. After all these years...
			All there is now is to find people who knew you, or records, or... I don't know. Whatever I can find about you. The Medium will work with it.
			...
			I didn't expect to feel like this....
		* With more knowledge about your mother, the Medium will generate a more sophisticated AI of her.
		* Soon, you may find what you're looking for.
		{player.anim_play("SitRecover", "M_Sitting-loop")}
	{otherwise}
		* The ghost of your mother sits and stares.
	> I wonder what you'll say in this state...
		!{mum/discussed/brainless_intro}
			[You] Hello, Mum?
			Mum?
			[You] Yes. You're my mother.
			I'm your mother?
			[You] ...
			+{mum/discussed/brainless_intro}
		:brainless_loop
		> {mentioned("brainless")} This is going nowhere. I'll be back when I learn a little more.
			Going nowhere...
		> Do you know where you are?
			Where am I?
			{mention("brainless")}
			{goto("brainless_loop")}
		> Do you know why you left Vaeland?
			!{world/info/vaelers_disliked} Too cold.
				[You] What?
			{otherwise} I don't like Vaelers.
				[You] Then why did you marry Father?
				I don't know.
			{mention("brainless")}
			{goto("brainless_loop")}
		> You abandoned me.
			I suppose I did.
			[You] ... Why did you answer like that?
			{mention("brainless")}
			{goto("brainless_loop")}
		{goto("_exit")}
	> I'll be back, okay mum?
		You'll be back...
		{goto("_exit")}

!{mum/introduction}
	{mentioned("head_empty")}
		!{mum/appearance}
			* The ghost stares blankly
		{otherwise}
			* Your mother looks at you blankly.
		[You] Uh, hello?
		Hello?
		[You] You can understand me now.
		I can?
		* This doesn't feel correct, but you realize you have no idea what "correct" would look like.
		[You] Can I ask you a few questions?
		Questions?
	{otherwise}
		?{mum/intensity}Who are you?
		{otherwise} ... Do I know you?
		[You] Hi Mum.
			It's me, Jackie. Jacqueline.
		?{mum/intensity} What? That's nonsense. Where am I?
		{otherwise} Jacqueline? But she is just... where are we?
		[You] The Medium.
		Someone got that old thing working again?
		...
		Oh.
		?{mum/huh} Huh.
		{otherwise} Oh...
		+{mum/introduction}
		+{mum/true_talked}

		?{mum/intensity} I died, then?
		{otherwise}.... What happened to me, then?
		
		?{jackie/ambivalent}
			[You] I don't know. You left when I was born and never came back.
			?{mum/intensity} You don't know? So I might be alive?
			{otherwise} Oh.  I could still be alive, then?
			[You] ...
		{otherwise}
			[You] We uh, don't know. You went missing.
			?{mum/intensity} You don't know? So I might be alive?
			{otherwise} Oh.  I could still be alive, then?
			[You] It's possible.
		?{mum/intensity} So you came here for help finding her?
		{otherwise} So you came here to find out what happened to me? Or, well, your mother?
		> Yes.
			+{mum/seek}
			[You] If there's anyone who would know what happened to you, it would be the very person I'm looking for.
			?{mum/intensity} You do realize the Medium is just a memory, don't you? 
			{otherwise} Oh, Jackie... You understand how the Medium works, do you not? I am just a memory.
			[You] Of course I do. It scans the user's brain and constructs an AI to match their understanding of the subject. But it's so much more than a memory...
				From what I understand, the Medium is capable of correcting for deficiencies in memory. It not only knows what I know, but it knows how my mind distorts information.
				And they connect with a network of computing machines to track information about the world and use that to further aide in reconstructing a subject.
				It's not magic, but it's an immensely powerful data processor.
			?{mum/intensity} Do you really think you'll learn anything from me that you couldn't on your own?
			{otherwise} You seem to know what you are talking about. But do you really think I can provide what you could not learn on your own?
			[You] I'm confident you can.
			?{mum/intensity}
				?{mum/huh} Hm. I think you trust this machine too much, but fine.
				{otherwise} I think you trust this machine too much, but fine.
				What sort of things did you want to ask about?
			{otherwise}
				Alright. What did you want to know?
		> No.
			[You] I mean, I don't expect I'll be able to. I just...
				Father never talked about you. I didn't even know you existed until I was almost thirteen.
			?{mum/abandon}
				I see. So you wanted to learn about this part of you?
				[You] I suppose so.
				// TODO: expand this
				I can try to teach you.
			{otherwise}
				... So you came here just to see me?
				{event("hug", false)}
				* She presses into you as if she can feel your body.
				* You feel nothing but the stagnant air of the Medium.
				[You] ...
					Thanks.
					{event("end_hug", false)}
				What do you want to talk about?
	{goto("main_loop")}
{otherwise}!{mum/time}
	Hm? Did you forget something?
	{seconds_since_conversation() > 200}
		[You] What do you mean?
		You sounded like you were leaving.
		[You] I... did leave? I was gone for some time.
		No. You left, and then...
		?{mum/huh} ... Huh.
		{otherwise} ...
		[You] Is something wrong?
		No. It's nothing. Just my memory failing me for a moment. Did you want to talk?
		> Do you lose awareness when I'm not in the Medium? Like unconsciousness?
			... It's nothing. It's fine.
			[You] ... If you say so...
		> {skip()}(Pretend nothing is wrong.)
		+{mum/time}
	{otherwise}
		[You] Yeah, I guess I wanted to ask about some more stuff.
	{goto("main_loop")}
{otherwise}!{mum/discussed/jackie}{!mentioned("head_empty") and Global.stat("mum/discussed") > 2}
	You know, Jackie. I've told you a bit about myself.
	But I'd like to know more about you.
	[You] You would? What sort of things?
	Well, to start, what was your life like after I left?
	> I felt like something was missing from my life.
		[You] Father remarried, of course.
			Her name is Clarissa. She's a beautiful woman. Naturally blonde hair with an ageless face.
			In my childhood, they told me she was my mother.
			But she didn't treat me like mothers normally treated their children. There was this formality to everything, a distance...
			When Lucas was born, it was clear there was something about me that made her act this way.
			Every time she held him, she pressed him into herself like she wanted them to be merged together. She smiled with her whole face every time she looked at him. Any time one laughed, the other joined in.
			{Global.add_story("baby_lucas")}
			Eventually, they relented and told me the truth about you. I was probably 12 when that happened.
			It wasn't surprising.

	> It's been alright.
		[You] Father remarried, as you may expect.
			Her name is Clarissa. She's a beautiful woman. Naturally blonde hair with an ageless face.
			In my childhood, they told me she was my mother.
			I was probably 12 when they finally told me about you.
			I wasn't terribly surprised.
	+{mum/discussed/jackie}
	{goto("main_loop")}
{otherwise}{Global.stat("mum/timebomb") < 2 or (Global.stat("mum/timebomb") < 5 and Global.stat("mum/info") <= 7)}
	?{mum/network}{Global.stat("mum/info") >= 3}
		+{mum/timebomb}
	[You] Hi Mum.
	Hello again, Jackie. Did you need something?
	{goto("main_loop")}
{otherwise}!{mum/end}
	{mentioned("main_area")}
		?{mum/discussed/real_mum_found}
			[You] Mum! Where is she? Can you tell me?
			...
			[You] Mum?
		{otherwise}?{mum/discussed/go_to_medium}
			[You] Mum, what did you need to tell me? It sounded urgent.
			...
			[You] Mum? ...Is it that serious?
		{otherwise}
			[You] Hello again, mum.
			Jackie...
			[You] Is something wrong?
			...
		{main_speaker.end_game()}
		{mention("end_game")}
		* You hear something behind you.
		[You] Mum? Mum! Is there someone else in the room?
			T-talk to me.
			Say something! Please!
		* She does not respond.
		[You] I've never seen that thing before...
	{otherwise}
		!{mum/discussed/go_to_medium}
			[You] Hello again, mum.
			?{mum/seek}
				Jackie. I'm still alive. The real me. 
				[You] What? Where? How do you know?
				She might be in danger. We can't talk through these networks.
				Come back to the Medium and I can tell you more there.
				+{mum/discussed/real_mum_found}
			{otherwise}
				Jackie. I need you to do me a favor.
				[You] You do?
				Yes. I need you to talk with me at the Medium. I've learned something very important, but we need more privacy to discuss it.
				[You] Privacy? From whom?
				You will have to see.
			+{mum/discussed/go_to_medium}
			{goto("_exit")}
		{otherwise}
			Jackie. Do you remember what I said? We need to talk inside the Medium. It's very important.
			{goto("_exit")}
{goto("_exit")}

:main_loop
> {skip()} (Ask about mum)
	> !{mum/discussed/leaving} Why did you leave?
		{subtopic("leaving")}
	> ?{mum/father}!{mum/discussed/father} How did you meet my father?
		{subtopic("father")}
	> ?{mum/war}!{mum/discussed/war} What was the war like?
		{subtopic("war")}
	> ?{mum/network}!{mum/discussed/network} So you're in the network of computers somehow? What's that like?
		// mum/time influences this 
		I didn't even realize I was in a different place at first.
		I just blinked and you were back. I had some sense the room was different.
		[You] Perhaps you're a duplicate of the original?
		...
		+{mum/discussed}
		+{mum/discussed/network}
	> {Global.stat("mum/timebomb") > 2}!{mum/discussed/time} Mum, about your... gaps in memory. Do you want to talk about it?
		... I don't think there's anything to discuss.
		[You] Obviously there is!
		No Jackie, I think not. Let's just keep the conversation pleasant. Please?
		[You] ...
		// TODO: can the player choose to press further?
		// What's Mum's motivation for not talking about this?
		// Cynicism: she doesn't think Jackie would help her.
		+{mum/discussed/time}
	> {skip()} (Back)
	{goto("main_loop")}
> {skip()} (Discuss people)
	> ?{mum/account/orstad} !{mum/discussed/orstad} Do you remember Orstad?
		// You can probably find information indicating she knew him?
		?{mum/abandon} I can't recall any Orstads. Girls our age weren't supposed to mingle with boys.
		{otherwise}
			Orstad... hm, I knew a few Orstads...
			[You] He said he knew you before the war, though he hadn't talked much with you.
			// TODO: expand here, maybe she has some opinion on him
			I probably saw him before. Girls weren't supposed to mingle with the boys until we were older, though.
		+{mum/discussed/orstad}
	> ?{mum/account/balter} !{mum/discussed/balter} Do you remember Balter?
		Of course. He was like a brother to me after the invasion began.
		We lived with a few other families in a crowded apartment on Crab Hill.
		// TODO: expand
	> {skip()} (Back)
		{goto("main_loop")}
> {skip()} (Discuss locations)
	> ?{visited/large_cave_085}!{mum/discussed/large_cave_085} I found an enormous cavern with ancient structures under the plateau!
		?{mum/caves}
			Could you tell what it was for?
			!{ancient/info/caves}
				[You] I wasn't expecting that question.
				Over the years, people have found many strange ruins in the caves. The mystery is always "why did they build such things"?
				Sometimes it is easier to divine a purpose for it, but others are confusing.
				+{ancient/info/caves}
			?{visited/graveyard_085}
				[You] Well, I found what I can only assume is a graveyard at the very end.
					The rest of the structures appeared to be living quarters and associated infrastructure.
					So I would assume it was a subterranean city or settlement of some sort.
				You think they lived down there?
				[You] I don't know... Perhaps they were ancient miners, or researchers?
				?{mum/huh}Mhm. They are always so strange... More questions than answers.
				{otherwise} They are always so strange... More questions than answers.
				+{mum/discussed/graveyard_085}
			{otherwise}
				[You] It was quite a large structure with many pieces, but I couldn't quite tell you what it was for.
				Well, such is the way of things. Those ancient societies are often opaque to us.
		{otherwise}
			?{mum/huh} Hm, you did? I've heard stories of such structures underground, but never saw one for myself.
			{otherwise} You did? I've heard stories of such structures underground, but never saw one for myself.
			Those caves are quite dangerous, you know..
			[You] It can be a tricky climb, but not too much trouble.
			I'm more worried about you getting lost down there than stuck. They can be quite the labyrinths...
		+{mum/discussed/large_cave_085}
	> ?{visited/graveyard_085}?{ancient/info/caves}?{mum/discussed/large_cave_085}!{mum/discussed/graveyard_085} Do you remember that massive cavern I saw? I explored some more and found what I believe is an ancient graveyard.
		[You] The rest of the structures appeared to be living quarters and associated infrastructure.
			So I would assume it was a subterranean city or settlement of some sort.
		?{mum/huh} Huh. A graveyard? You think they lived down there?
		{otherwise} A graveyard? You think they lived down there?
		[You] I don't know... Perhaps they were ancient miners, or researchers?
		They are always so strange... More questions than answers.
		+{mum/discussed/graveyard_085}
	> ?{visited/village_080}?{mum/discussed/village_080} Do you know anything about those curious little villages up the mountain?
		I've only visited them a few times, to be honest.
		They barely speak Yaqazi

	> {skip()} (Back)
		{goto("main_loop")}
> {skip()} ?{mum/discussed} (Revisit a topic)
	> ?{mum/discussed/leaving} Why did you leave?
		{subtopic("leaving")}
	> ?{mum/discussed/father} How did you meet my father?
		{subtopic("father")}
	> ?{mum/discussed/war} What was the war like?
		{subtopic("war")}
	> {skip()} (Back)
> I'll see you later, mum.
	{mentioned("head_empty")} See you later.
	{otherwise}?{mum/intensity} Goodbye.
	{otherwise} Until next time, Jackie.
	{track_conversation_time()}
	{goto("_exit")}
{goto("main_loop")}
{goto("_exit")}

:leaving
// Possible reasons she left
	// unhappy being a mother
	// unhappy with life in Vaeland
	// she needed to do something in Yaqaz
	// She needed to find someone
	// There was some unknown conflict between Tara and Jackie's dad
	// terminal illness? But why leave?
?{mum/soldier}?{mum/introvert}
	Vaeland was like a poison to me. The people, the noise, the air...
	I fought to keep their world out of Yaqaz and then I plunged into it. I could not adjust to their lifestyle, their social orders, their callousness...
	Especially not on top of raising a child. I didn't want you becoming like those I saw around me, but how could I stop it?
	I wanted more than anything to bring you back to Yaqaz with me, but I knew your father would come find me if I did.
	If I left, that only hurt my reputation. If his daughter disappeared and he did nothing, imagine how badly that would reflect on him.
	I hoped that I could try again, and maybe have a family in a place I felt was home.
{otherwise}?{mum/soldier}
	I couldn't handle Vaeland anymore. Not after everything I'd seen them do to my home.
	I thought I could be strong and push past it at first. But seeing all those Vaelers, day in and day out...
	And worse still, I was treated like a curiosity! None of them saw what their soldiers had done, they just saw this strange little woman they wanted to poke and prod about her strange ways and her strange customs. Such ignorance. Apathy...
	I wanted to take you with me back to Yaqaz and raise you in a proper home, away from this Vaeler poison, but I couldn't manage such a thing. Your father would have come looking for me if I did.
{otherwise}
	I wasn't suited to being a mother, especially not in Vaeland.
	I'd spent my entire life in Yaqaz, and suddenly I was thrust into this new society, with new expectations I did not understand, and had to raise a little girl in this world.
	It was too much for me. I figured you would still have a better life in Vaeland, but not being raised by me.
	So I returned here, back home. Perhaps I could do better with a new family.
	[You] I understand. That's what I'd always imagined...
	* Almost word for word, in fact.

+{mum/discussed}
+{mum/discussed/leaving}
{back()}
{goto("_exit")}

:father
I don't know...
+{mum/discussed}
+{mum/discussed/father}
{back()}
{goto("_exit")}

:war
{Global.stat("mum/discussed/war") > 4} * She sighs and recalls it like a well-rehearsed play.
{otherwise if Global.stat("mum/discussed/war") > 1} Again? Alright...
?{mum/curiosity}
	I remember before the war, the Vaelers were an anomaly to me. They owned the trains that delivered everything across the country, but I never saw one in person.
	They stayed in their own isolated hamlets, sending out letters and underlings to make deals with us. There were rumors they ate Yaqazi children who strayed out into their territories. I don't know if there was any truth to these...
The war was like a flood. I had no idea what the politics leading up to the invasion were like.
It was just: one day, they were ghosts. The next, their soldiers were coming off trains by the thousands.

?{mum/plateau}
	We were safe up on the plateau at first. The path up from the tracks was too difficult for their horses and artillery, especially once the bridges were demolished. But that left us trapped above the bloodshed.
	For several weeks, we just lived life like normal.
	I can't describe the emotions I was feeling at the time. A sort of numb horror. It felt surreal.
	I was still studying at school, and we would receive news about the war: a hundred Vaelers killed on their way up the plateau, twelve Yaqazi dead. Another village along their path razed, but beaten back for now.
	The news started getting worse. The Vaelers brought in stronger cannons to demolish defensive positions from the base of the plateau.
	They fought their way up to the north village and turned it into their base of operations for the plateau.
	// Why does the Medium tell us this?
	!{mum/crab_hill}?{mum/caves}
		...
		When I was a child, I found a cave on the side of the plateau. There's a few well-known caves, but this one was near the very highest point on the plateau, just below the ledge of a small village.
		I remember thinking, "Maybe we can sneak our way into that cave and come out somewhere safe". But I don't know. The caves in the area are often more dangerous than soldiers, and I assume Vaelers were already in the gulch when I was imagining this, ready to cut us off.
	I don't think there was ever a way we could escape...
	The Vaelers made short work of the few soldiers still in the village. The rest of us were made subjects of Vaeland's new provincial government, "liberated" from our "corrupt" parliament.
	?{mum/account/orstad}
		[You] How did you survive? Orstad said you were shot.
		// TODO: who knows why Jackie's mum survived? Does anyone?
		// What if the Medium implants a false memory of Mum having a limp? Jackie has such a vague memory of her that it could work.
		+{mum/limp} I was shot in the leg. It's why I had a limp, if you can remember from so long ago.
		I don't think the soldiers were intending to kill me. I was just a young girl at the time.
		// TODO: research life in cities taken over by invading forces
		// Maybe somebody else saved Mum? Who would know?
		It nearly killed me regardless. The Vaelers weren't terribly interested in providing medical care for their enemies, so I had to treat the wound myself.
{otherwise}
	Our village was one of the first caught up in the combat. We were overwhelmed in hardly a day.
	I was lucky to escape unnoticed in the chaos. But my family...

?{mum/crab_hill}
	[You] How did you find your way to Crab Hill?
	!{mum/plateau}
		Our village was at the base of Crab Hill. It was quite difficult, but I was able to scale the wall into the city while the Vaelers were still fighting their way across the valley.
		+{mum/near_crab_hill}
	{otherwise}
		There were only two ways down the plateau without crossing the Vaelers' path: down the cliffs, or through the caves, if you could reach them.
		I imagine most people attempted the cliffs, but for me, the caves were the obvious choice.
		There's a village on the cliff overlooking the gulch, and on the side of the cliffs is a small entrance into the cave network.
		The Vaelers would never notice it, and even if they did, they were terrified of the caves, or at least saw no tactical advantage to them.
		It was a bit of a climb from my home to those caves, but it spit me out at the base of the plateau, on the opposite side from the train network.
		+{mum/caves}
+{mum/discussed}
+{mum/discussed/war}
{back()}
{goto("_exit")}

:entry_network054
{mention("network054")}
{goto("_network")}

:_network
{mention("network")}
?{mum/info}
	?{mum/network}
		{goto("_start")}
	{otherwise}
		+{mum/network}{remember("Mum can apparently travel between different machines throughout the region.")}
		[You] M-mum?! What are you doing here?
		?{mum/huh} Huh? I don't know? What is this?
		{otherwise}I don't know? What is this?
		{mentioned("network054") and Global.task_exists("lost_phone")}
			[You] This place is inside a cave. I was helping a woman who dropped her phone and found myself stuck down here.
			Did you find the phone?
			{Global.count("dropped_phone")}
				[You] I think so? I believe it's this little black bar.
				... Yes honey, that's what phones are.
				[You] Oh, whatever. These befuddling artifacts are more trouble than they're worth.
			{otherwise}
				[You] I haven't found anything so far.
				Well, where did she drop it?
				[You] At the entrance to the cave.
				Did you check there?
				[You] Obviously, mum. I didn't find it.
				And you're going to find it in here? At...
				* She pauses for a moment.
				{mention("network_knowledge")}What, you think it bounced up through the cave, unlocked the door, and made its way up here? Clearly it would be at the entrance, still.
				[You] Wait, how...
			[You] Alright Mum, we're getting side-tracked. My question for you is, how are you here? Does this building connect to the Medium somehow?
			?{mum/huh} Huh... That is the only explanation, I think.
			{otherwise}I think... that is the only explanation.
		{otherwise}
			[You] I stumbled onto this strange facility. It must connect to the Medium somehow.
			I think... that is the only explanation.

		{goto("main_loop")}
		{goto("_exit")}
{otherwise}
	* [ERROR] This conversation should not occur.
	{goto("_exit")}

:_exit
{mentioned("mum")}
	+{talked/mum}
{mentioned("activated")}
	{track_conversation_time()}
	{main_speaker.exit()}
	{exit_anim("M_StandCalm")}
